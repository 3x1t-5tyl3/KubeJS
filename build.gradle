plugins {
	id "architect-plugin" version "1.2.33"
	id "forgified-fabric-loom" version "0.5.13" apply false
}

architect {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: "forgified-fabric-loom"

	loom {
		silentMojangMappingsLicense()
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architect-plugin"
	apply from: "https://files.latmod.com/public/markdown-git-changelog.gradle"

	archivesBaseName = rootProject.archives_base_name
	def ENV = System.getenv()
	version = ENV.BUILD_NUMBER ? "${rootProject.mod_version}.${ENV.BUILD_NUMBER}" : "${rootProject.mod_version}.custom"
	group = rootProject.maven_group

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}

	repositories {
        mavenLocal()
		maven {
			url "https://www.cursemaven.com"
			content {
				includeGroup "curse.maven"
			}
		}
	}
}